//// Hx.pgpre: HX PARSER PRE TERM PRODUCTIONS.
//// Copyright Â© 2012,2014 Kristoffer H. Rose <krisrose@rcsx.org>


//// STRUCTURE.

<HxModule> ::= {top-module}:rawhx <HxEmbeddedModule> .

<HxEmbeddedModule> ::= HX_MODULE:rawhx <HxModuleName> HX_LBRACE <HxStoring_Declarations> HX_RBRACE .

<HxDeclarations> ::= ( {$Cons} <HxDeclaration> <HxDeclarations> | {$Nil} ).

<HxDeclaration> ::=
  {embedded-module}:rawhx <HxEmbeddedModule> HX_SEMI
| HX_IMPORT:rawhx <HxModuleName> ( HX_LPAREN (<HxSortNames> | {$Nil}) HX_RPAREN | {$Nil} ) HX_SEMI

| HX_SPACE:rawhx <HxRegExp> HX_SEMI
| HX_TOKEN ( {'token'}:rawhx <HxStore_SortName> HX_BAR <HxRegExp> HX_SEMI | HX_FRAGMENT:rawhx <HxFragmentName> HX_BAR <HxRegExp> HX_SEMI )
| HX_FRAGMENT:rawhx <HxFragmentName> HX_BAR <HxRegExp> HX_SEMI

| HX_ATTRIBUTE:rawhx <HxAttributeKind> <HxAttributeName> <HxAttributeForm> <HxAttributeOptions> HX_SEMI

| {sort}:rawhx (HX_MAIN:rawhx|{no-main}:rawhx) HX_SORT <HxStore_SortName> <HxRepeat> <HxSortAbstraction> HX_SEMI
| {anonymous}:rawhx <HxSortAlternatives> HX_SEMI
| (HX_RULE)? {rule}:rawhx <HxRule> HX_SEMI
| {nestedDeclarations}:rawhx HX_LBRACE <HxDeclarations> HX_RBRACE
.

<HxAttributeKind> ::= ( {AttributeKindUp}:rawhx HX_UP | {AttributeKindDown}:rawhx HX_DOWN ).


//// TOKEN DECLARATIONS.

<HxRegExp> (HX_RE) ::= {$Cons} <HxRegExpChoice> ( HX_BAR <HxRegExp> | {$Nil} ) .

<HxRegExpChoice> (HX_RE) ::=
  HX_NESTED:rawhx <HxRegExpSimple> <HxRegExpSimple>
| {RegExpConcat}:rawhx <HxRegExpUnits>
.

<HxRegExpUnits> (HX_RE) ::= {$Cons} <HxRegExpUnit> <HxRegExpUnits> | {$Nil} .

<HxRegExpUnit> (HX_RE) ::= {RegExpUnit}:rawhx <HxRegExpSimple> <HxRepeat> .

<HxRepeat> (DEFAULT,HX_RE) ::=
  HX_PLUS ( {RepeatSomeSep}:rawhx HX_SUB (<HxRegExpLeaf> | {RegExpWord}:rawhx <HxSeparator>) | {RepeatSome}:rawhx )
| {RepeatMaybeSome}:rawhx HX_STAR
| {RepeatMaybe}:rawhx HX_QUEST
| {RepeatSingle}:rawhx
.

<HxRegExpLeaf> (DEFAULT,HX_RE) ::=
  {RegExpString}:rawhx <HxString>
| {RegExpWord}:rawhx <HxWord>
.

<HxRegExpSimple> (HX_RE) ::=
  <HxRegExpLeaf>
| {RegExpRef}:rawhx <HxFragmentRef>
| {RegExpClass}:rawhx <HxRegExpClass>
| {RegExpAny}:rawhx HX_DOT
| {RegExpClass}:rawhx {RegExpClassChar}:rawhx (HX_ESCAPEDCHAR$) {RegExpClassDone}:rawhx
| {RegExpNest}:rawhx HX_LPAREN <HxRegExp> HX_RPAREN
.


//// ATTRIBUTE DECLARATIONS

<HxAttributeForm> ::=
  {AttributeFormSimple}:rawhx HX_LPAREN <HxSort> HX_RPAREN
| HX_LBRACE <HxSort>:# ( {AttributeFormMap}:rawhx {{#}} HX_COLON <HxSort> HX_RBRACE
                  | {AttributeFormSet}:rawhx {{#}} HX_RBRACE )
.

<HxAttributeOptions> ::= {$Cons} <HxAttributeOption> <HxAttributeOptions> | {$Nil} .
<HxAttributeOption> ::= {AttributeOption}:rawhx <HxVariable> <HxConstructor> .


//// SORT PRODUCTION DECLARATIONS 

<HxSortNames> ::= {$Cons} <HxSortName> (HX_COMMA <HxSortNames> | {$Nil}) . 

<HxSortAbstraction> ::=
  {SortAbstractionParam}:rawhx <HxVariable>^a <HxSortAbstraction>[a]
| {SortAbstractionBody}:rawhx <HxSortAlternatives>
.

<HxSortAlternatives> ::= {$Cons} <HxSortAlternative> <HxSortAlternatives> | {$Nil} .

<HxSortAlternative> ::= HX_BAR (
  HX_SCHEME:rawhx <HxForm>
| {SynthesizedRef}:rawhx HX_UP <HxAttributeName> 
| HX_SUGAR:rawhx <HxForm> HX_ARROW <HxTerm>
| HX_SYMBOL:rawhx <HxForm>
| HX_STATIC:rawhx (HX_SYMBOL)? <HxForm>
| {data}:rawhx (HX_DATA)? <HxForm> <HxTagging>
).

<HxForm> ::=
  <HxConstructor>:#C
    ( {FormParsedSorted}:rawhx {{#C}} <HxParsedForm> <HxFormPrec> <HxInheritedRefs>
    | {FormConstruction}:rawhx {{#C}} ( HX_LPAREN (<HxBindersScopeSorts> | {$Nil}) HX_RPAREN | {$Nil} ) <HxInheritedRefs>
    )
| {FormParsed}:rawhx <HxParsedForm> <HxFormPrec> <HxInheritedRefs>
.

<HxBindersScopeSorts> ::= {$Cons} <HxBindersScopeSort> (HX_COMMA <HxBindersScopeSorts> | {$Nil}) .

<HxBindersScopeSort> ::=
  <HxSort>:#Sort
   ( {BinderScopeSort}:rawhx HX_BINDS <HxVariable>!!! {{#Sort}} <HxBindersScopeSort>
   | {ScopeSort}:rawhx {{#Sort}} <HxRepeat> (HX_LBRACKET <HxSubstituteSorts> HX_RBRACKET | {$Nil})
   )
.

<HxSubstituteSorts> ::= {$Cons} <HxSubstituteSort> ( HX_COMMA <HxSubstituteSorts> | {$Nil} ) | {$Nil}  . 
<HxSubstituteSort> ::= <HxVariable>!!!:#v ( {SubstituteSort}:rawhx {{#v}} (HX_COLON|HX_AS) <HxSort> |
  {SubstituteNoSort}:rawhx {{#v}} ) .

<HxSort> ::= {Sort}:rawhx <HxSimpleSort> <HxSimpleSorts> .

<HxSimpleSorts> ::= HX_LPAREN ( {$Cons} <HxSimpleSort> <HxSimpleSortTail> | {$Nil} ) HX_RPAREN | {$Nil} .
<HxSimpleSortTail> ::= HX_COMMA {$Cons} <HxSimpleSort> <HxSimpleSortTail> | {$Nil} .

<HxSimpleSort> ::=
  {SortName}:rawhx <HxSortName> <HxMetaVariables>
| {SortParam}:rawhx <HxSortParam>
.
<HxMetaVariables> ::= {$Cons} <HxMetaVariable> {$Nil} | {$Nil} .
<HxSortParam> ::= <HxVariable>! .

<HxScopeSortPrecRepeat> ::= {ScopeSortPrecRepeat}:rawhx <HxScopeSort> <HxFormPrec> <HxRepeat> .
<HxScopeSort> ::=
  <HxSort>:#Sort
   ( {ScopeSortBinder}:rawhx HX_BINDS <HxVariable>!!! {{#Sort}}
   | {ScopeSort}:rawhx {{#Sort}} <HxRepeat> (HX_LBRACKET <HxSubstituteSorts> HX_RBRACKET | {$Nil})
   )
.

<HxFormPrec> ::= ( HX_AT <HxNatural> | {"0"} ) .

<HxTagging> ::= ( {$Cons} HX_TAG <HxConstructor> ({$Nil}) | {$Nil} ).

<HxInheritedRefs> ::=( {$Cons} <HxInheritedRef> <HxInheritedRefs> | {$Nil} ).
<HxInheritedRef> ::= {InheritedRef}:rawhx HX_DOWN <HxAttributeName> . 

//// RULES.

<HxRule> ::= {Rule}:rawhx <HxRulePrefix> <HxNotVariableTerm> ( HX_ARROW <HxTerm> | {OMITTED}:rawhx ) .

<HxRulePrefix> ::= {RuleOptions}:rawhx <HxPriority> ( HX_LBRACKET (<HxOptions> | {$Nil}) HX_RBRACKET | {$Nil} ) .

<HxPriority> ::=( HX_DEFAULT:rawhx | HX_PRIORITY:rawhx | {Normal}:rawhx ).

<HxOptions> ::= {$Cons} <HxOption> ( HX_COMMA <HxOptions> | {$Nil} ) .

<HxOption> ::=
  {OptionConstruction}:rawhx <HxConstructor> ( HX_LBRACKET (<HxOptions> | {$Nil}) HX_RBRACKET | {$Nil} ) <HxAttributes>
| {OptionLiteral}:rawhx <HxLiteral> <HxAttributes>
.
